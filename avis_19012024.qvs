///$tab Main
///tab Main
SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00р.;-# ##0,00р.';
SET TimeFormat='h:mm:ss';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY h:mm:ss[.fff]';
SET MonthNames='янв;фев;мар;апр;май;июн;июл;авг;сен;окт;ноя;дек';
SET DayNames='Пн;Вт;Ср;Чт;Пт;Сб;Вс';

$(Must_Include=\\z11-00759-qv\QVProject\00-00 script parameters.qvs);
$(Must_Include=\\z11-00759-qv\QVProject\00-05 Finance Parameters.qvs);

SET перегруз_авис = 1;

SET обновление_подоговорно = 1;
SET vIsLoadFROMDB = 0; //настройка загрузки из БД (1) qvd (0) 

///tab Авис
IF перегруз_авис = 1 THEN 

	IF vIsLoadFROMDB = 1 THEN
		//промышленая REPORT_RNP_QV_ALL отключен
		OLEDB CONNECT TO [Provider=SQLOLEDB.1;Integrated Security=SSPI;Persist Security Info=False;Initial Catalog=$(vAVISSchema);Data Source=$(vAVISDataSourceRepl);Use Procedure for Prepare=1;Auto Translate=True;Packet Size=4096;Workstation ID=Z11-0229-QLIKW;Use Encryption for Data=False;Tag with column collation when possible=False];
		[Авис]:
		SQL SELECT 
			*
		FROM dbo."REPORT_RNP_QV_ALL";
	ELSE 
		[Авис]:
		LOAD 
			*
		FROM [$(vAvisDir)\report_rnp_qv_all.qvd] (qvd);
	ENDIF;

	// 05.06.2017 krasovku a-centre 603
	Left Join ([Авис])
	LOAD
		[econ_id], 
		[econ_date]
	FROM [$(vAvisDir)\mav_parus_econ.qvd] (qvd);

	TRACE [Авис] into [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\авис.qvd] (qvd);
	STORE [Авис] into [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\авис.qvd] (qvd);
	DROP Table [Авис];

ENDIF; 

[Журнал_авис]:
LOAD 
	[company]					as [код филиала], 
	Text( [id_department])		as [подразделение код], 
	[name]						as [подразделение название], 
	[channel_id], 
	Text([channel_info])		as [канал название], 
	[ag_id]						as [Договор ID], 
	[ag_our_number]				as [номер корневого договора], 
	[ag_our_number]				as [Договор Номер],
	'ДМС/общий'					as [ТОС назнвание],
	[insurer]					as [Страхователь], 
	Date([op_date])				as [дата начисления], 
	Date([econ_date])			as [дата подтверждения],
	[opr_sum_r]					as [сумма], 
	[opr_sum_d]					as [сумма_депозит],
	'Премия'					as [Тип],
	'Авис'						as [система],
	Date( If(IsNull([am_start_date]), [op_date], [am_start_date]))  as [дата начала ответсвенности],
	'1'							as [Тип_перестрахования]//,
//	'PP0799'					as [Продукт OEBS]
FROM [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\авис.qvd] (qvd)
Where
	([ac_credit] = '92/1Н')
	and 
	([is_loaded] <> 'не начисл. в Парус' or IsNull([is_loaded]));

Concatenate ([Журнал_авис])
LOAD 
	[company]				as [код филиала], 
	Text([id_department])	as [подразделение код], 
	[name]					as [подразделение название], 
	[channel_id], 
	Text([channel_info])	as [канал название], 
	[ag_id]					as [Договор ID],
	[ag_our_number]			as [номер корневого договора],
	[ag_our_number]			as [Договор Номер], 
	'ДМС/общий'				as [ТОС назнвание],
	[insurer]				as [Страхователь], 
	Date([op_date])			as [дата начисления], 
	Date([econ_date])		as [дата подтверждения],
	-[opr_sum_r]			as [сумма], 
	-[opr_sum_d]			as [сумма_депозит],
	'расторжение'			as [Тип],
	'Авис'					as [система],
	Date( If(IsNull([am_start_date]), [op_date], [am_start_date]))	as [дата начала ответсвенности],
	'1'						as [Тип_перестрахования]//,
	//'PP0799'				as [Продукт OEBS]
FROM [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\авис.qvd] (qvd)
Where
	([ac_debit] = '22/5Н' or [ac_debit] = '91/17')
	and 
	([is_loaded] <> 'не начисл. в Парус' or IsNull([is_loaded]));

Left Join ([Журнал_авис])
LOAD 
	Text([код_подразделения])	as [подразделение код], 
	[Код OEBS ЦФО 2011]			as [Код ЦФО] 
FROM [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\справ_кодов_ЦФО.qvd] (qvd)
Where not IsNull([код_подразделения]);

Left Join ([Журнал_авис])
LOAD 
	[Тос название Юникус]		as [ТОС назнвание], 
	[Номер строчки], 
	Text([Код OEBS АВС 2011])	as [Код АВС]
FROM [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\справ_кодов_АВС.qvd] (qvd)
Where not IsNull([Тос название Юникус]);

Left Join ([Журнал_авис])
LOAD 
	[КП название Юникус]	 as [канал название], 
	Text([Код OEBS КП 2011]) as [КП]
FROM [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\справ_кодов_КП.qvd] (qvd)
Where not IsNull([КП название Юникус]);

// GoryachkinAV 30.03.2023  выделение продукта по Сберу

Left Join ([Журнал_авис])
LOAD Distinct
	ag_id as [Договор ID],      
	groupe_id
FROM [$(vAvisDir)\agreement.qvd] (qvd);

Left Join ([Журнал_авис])
LOAD Distinct
	groupe_id,
	groupe_name
FROM [$(vAvisDir)\agreement_group_new.qvd] (qvd);

[Журнал_авис_продукт]:
LOAD 
	*,
	if( groupe_name like '*Сбербанк*', 
		Text('B301007'), 
		if( Wildmatch([номер корневого договора], '*/459/*','*/460/*') > 0 and Wildmatch([номер корневого договора], '*/045/459/*', '*/045/460/*') = 0, 
			Text('B301001'),  
			Text('B301000')
		)
	) as [Продукт OEBS]   //Goryachkin 22.04.23  добавляем продукт синопсис
Resident [Журнал_авис];

Drop table [Журнал_авис];
RENAME Table [Журнал_авис_продукт] to [Журнал_авис];

//end Сбер

TRACE [Журнал_авис] into [$(vFinanceDir)\Недельные отчеты\операции_авис.qvd] (qvd);
STORE [Журнал_авис] into [$(vFinanceDir)\Недельные отчеты\операции_авис.qvd] (qvd);

//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>20.01.2014-----------------------------------------------

[Avis_mapp]:
NoConcatenate
LOAD 
	[ag_our_number], 
	Text([Код OEBS АВС 2011])	as [АВС], 
	Text([Код OEBS ЦФО 2011])	as [ЦФО], 
	Text([КП 2011])				as [КП], 
	Text([Код ПП])				as [Код ПП],
	[Год договора OEBS],
	[Продукт OEBS]            
FROM [$(vFinMappingDirCurrent)\Эталон АВИС_ИТОГ.qvd] (qvd);

[Avis_mapp_GR]:
NoConcatenate
LOAD
	[номер корневого договора]				as [ag_our_number_GR],
	Text(FirstValue([Код АВС]))				as [Код OEBS АВС 2011],
	Text(FirstValue([Код ЦФО]))				as [Код OEBS ЦФО 2011],
	Text(FirstValue([КП]))					as [КП 2011],
	Text(FirstValue([подразделение код]))	as [Код ПП GR],
	Text(FirstValue([Продукт OEBS]))		as [Продукт OEBS]
Resident [Журнал_авис]
Group by [номер корневого договора];

//<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<20.01.2014-----------------------------------------------

DROP Table [Журнал_авис];

//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>20.01.2014-----------------------------------------------

Concatenate ([Avis_mapp])
LOAD
	[ag_our_number_GR]	as [ag_our_number],
	[Код OEBS АВС 2011]	as [АВС],
	[Код OEBS ЦФО 2011]	as [ЦФО],
	[КП 2011]			as [КП],
	[Код ПП GR]			as [Код ПП],
	Text('2')			as [Год договора OEBS],
	[Продукт OEBS]		as [Продукт OEBS]
Resident [Avis_mapp_GR]
Where not Exists([ag_our_number], [ag_our_number_GR]);

DROP Table [Avis_mapp_GR];

TRACE [Avis_mapp] into  [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\итого_мэпинг_авис.qvd] (qvd);
STORE [Avis_mapp] into  [$(vFinanceDir)\Обработка мэппингов для QV\Мэпинг АВС_ЦФО_ КП для QV\итого_мэпинг_авис.qvd] (qvd);
DROP Table [Avis_mapp];

//<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<20.01.2014-----------------------------------------------
